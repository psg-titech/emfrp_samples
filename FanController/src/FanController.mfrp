module FanController  # module name
in  tmp : Double,     # temperature sensor
    hmd : Double,     # humidity sensor
    clock : Int       # clock (second)
out di  : Double,     # discomfort index
    fan : Bool,       # fan switch
    ctime : Int       # cumulative operating time
use Std

# discomfort (temperature-humidity) index
node di = 0.81 *. tmp +. 0.01 *. hmd *. (0.99 *. tmp -. 14.3) +. 46.3

# fan switch
node init[False] fan = di >=. th

# hysteresis offset
node th = 73.0 +. (if fan@last then -.0.5 else 0.5)

node init[0] ctime = 
    if fan@last then clock - ctime0@last else ctime@last

node init[0] ctime0 =
    if !fan@last && fan
    then clock - ctime@last else ctime0@last
