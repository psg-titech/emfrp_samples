# Anti-Rotation Demo for Zumo32U4 / Zumo Robot for Arduino
# (adapted from RotationResist demo for Zumo32U4)
# Copyright (c) 2017 Takuo Watanabe

module AntiRotation
in  gyroZ : Int,    # gyroscope (z-axis)
    t     : Int     # current time (usec)
out motorL : Int,   # left motor
    motorR : Int    # right motor
use Std

# This function is used to constrain the speed of 
# the motors to be between -maxSpeed and maxSpeed
data maxSpeed = 400
func motorSpeed(s) = if s < -maxSpeed then -maxSpeed
                     else if s > maxSpeed then maxSpeed
                     else s

# PD-control parameters
data kp = 11930465 / 1000
data kd = 8

# time difference
node init[0] u = t
node dt = u - u@last

# Calculates the angle to turn from the gyroscope
# data and dt
node turnAngle = gyroZ * dt * 14680064 / 17578125

# Calculates the turning speed using a simple
# PD-control method.
node speed = motorSpeed(-turnAngle / kp - gyroZ / kd)

# Controls the motors
node (motorL, motorR) = (-speed, speed)
